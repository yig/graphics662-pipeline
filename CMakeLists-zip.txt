# A custom target to create a zip file to handin.

# Version 1: A custom target named "zip". Run "make zip" or "cmake --build build-dir --target zip"

## No extra large directories to exclude.
add_custom_target(zip COMMAND
    ${CMAKE_COMMAND} -E tar "cfv" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.zip" --format=zip
    ${SRCS}
    "include"
    "Notes.txt"
    "CMakeLists.txt"
    "CMakeLists-zip.txt"
    "${SCREENSHOTS}"
    "${EXAMPLES}"
    WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}"
    VERBATIM
)
add_dependencies(zip screenshots)


# Version 2: Describe the package to create. Run "make package" or "cpack".
# This depends on screenshots having been generated.

# Install only what we specify.
install(FILES
    "Notes.txt"
    "CMakeLists.txt"
    "CMakeLists-zip.txt"
    DESTINATION .
)
install(FILES
    ${SRCS}
    DESTINATION "src"
)
install(DIRECTORY "include"
    DESTINATION .
    PATTERN ".DS_Store" EXCLUDE
)
# No trailing slash after "${SCREENSHOTS}" preserves the "output" directory in the zip.
install(DIRECTORY "${SCREENSHOTS}"
    DESTINATION .
    PATTERN ".DS_Store" EXCLUDE
)
install(DIRECTORY "${EXAMPLES}"
    DESTINATION .
#     REGEX "${EXAMPLES}/head" EXCLUDE
#     REGEX "${EXAMPLES}/hercules" EXCLUDE
#     REGEX "${EXAMPLES}/lemon" EXCLUDE
    PATTERN ".DS_Store" EXCLUDE
)

set(CPACK_GENERATOR "ZIP")
set(CPACK_PACKAGE_FILE_NAME "${CMAKE_PROJECT_NAME}")
set(CPACK_INCLUDE_TOPLEVEL_DIRECTORY OFF)
set(CPACK_VERBATIM_VARIABLES YES)
include(CPack)
